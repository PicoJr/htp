weekday = { "monday" | "mon" | "tuesday" | "tue" | "wednesday" | "wed" | "thursday" | "thu" | "friday" | "fri" | "saturday" | "sat" | "sunday" | "sun" }
now = { "now" }
am_or_pm = { "am" | "pm"}
modifier = { "last" | "next" }
quantifier = { "min" | "hours" | "hour" | "h" | "days" | "day" | "d" | "weeks" | "week" | "w" | "months" | "month" }
shortcut_day = { "today" | "yesterday" | "tomorrow" }
mday = ${ (modifier)? ~ WHITE_SPACE* ~ weekday | shortcut_day }

relative = ${ int ~ WHITE_SPACE* ~ quantifier ~ WHITE_SPACE* ~ "ago"}
relative_future = ${ "in" ~ WHITE_SPACE* ~ int ~ WHITE_SPACE* ~ quantifier }
time = ${ hms ~ (":" ~ hms)? ~ (":" ~ hms)? ~ WHITE_SPACE* ~  am_or_pm?}
day_at = ${ mday ~ WHITE_SPACE* ~ ("at" ~ WHITE_SPACE* ~ time)?}
iso = ${ year ~ "-" ~ month ~ "-" ~ day ~ "T" ~ hms ~ (":" ~ hms)? ~ (":" ~ hms)? }

time_clue = {SOI ~ (now | iso | relative | relative_future | time | day_at) ~ EOI }

hms = { ASCII_DIGIT{1,2} }
year = { ASCII_DIGIT{4} }
month = { ASCII_DIGIT{2} }
day = { ASCII_DIGIT{2} }
int = { ASCII_DIGIT+ }
WHITESPACE = _{ " " }
